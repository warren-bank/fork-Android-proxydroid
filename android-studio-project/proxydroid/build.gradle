apply from: '../constants.gradle'
apply plugin: 'com.android.application'

android {
  compileSdkVersion project.ext.compileSdkVersion
  buildToolsVersion project.ext.buildToolsVersion
  ndkVersion        project.ext.ndkVersion

  compileOptions {
    sourceCompatibility project.ext.javaVersion
    targetCompatibility project.ext.javaVersion
  }

  defaultConfig {
    minSdkVersion project.ext.minSdkVersion
    targetSdkVersion project.ext.targetSdkVersion

    applicationId "org.proxydroid"
    versionName project.ext.releaseVersion
    versionCode project.ext.releaseVersionCode

    archivesBaseName += "-$versionName"

    ndk {
      abiFilters "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
      abiFilters.clear()
    }
  }

  flavorDimensions "languageResources"

  productFlavors {
    withAllLanguageTranslations {
      dimension "languageResources"
    }
    english {
      dimension "languageResources"
    }
  }

  buildTypes {
    release {
      debuggable      false
      jniDebuggable   false
      minifyEnabled   true
      shrinkResources true
      proguardFiles = [
        "proguard-rules.txt",
        getDefaultProguardFile('proguard-android.txt')
      ]
    }
    debug {
      debuggable      true
      jniDebuggable   true
      minifyEnabled   false
      shrinkResources false
    }
  }

  externalNativeBuild {
    cmake {
      version project.ext.cmakeVersion
      path "src/main/cpp/CMakeLists.txt"
    }
  }

  splits {
    abi {
      enable true
      reset()
      include "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
      universalApk true
    }
  }

  lintOptions {
    disable 'MissingTranslation'
    abortOnError true
  }
}

// https://stackoverflow.com/questions/57645919
configurations.all {
  resolutionStrategy.dependencySubstitution {
    substitute module('org.hamcrest:hamcrest-core:1.1') with module('junit:junit:4.10')
  }
}

dependencies {
  implementation 'com.googlecode.json-simple:json-simple:'  + project.ext.libJsonSimpleVersion  // ( 23 KB) https://mvnrepository.com/artifact/com.googlecode.json-simple/json-simple
  implementation 'org.mozilla:rhino:'                       + project.ext.libRhinoVersion       // (1.3 MB) https://mvnrepository.com/artifact/org.mozilla/rhino
}
